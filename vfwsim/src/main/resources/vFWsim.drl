/*-
 * ============LICENSE_START=======================================================
 * vFW simulator
 * ================================================================================
 * Copyright (C) 2017 AT&T Intellectual Property. All rights reserved.
 * ================================================================================
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *      http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 * ============LICENSE_END=========================================================
 */


package org.openecomp.policy.sim.vfw;

import org.openecomp.policy.appc.Request;

import org.openecomp.policy.controlloop.ControlLoopEventStatus;
import org.openecomp.policy.controlloop.ControlLoopTargetType;
import org.openecomp.policy.controlloop.VirtualControlLoopEvent;

import org.openecomp.policy.drools.system.PolicyEngine;

rule "vFWsim.ONSET"
when
	$onset : OnsetEvent()
then
	String WHERE = drools.getRule().getPackage() + "." + drools.getRule().getName();
	
	try {
		System.out.println(WHERE + ": " + "DCAE[ONSET|" + $onset.requestID + "|" + 
		                   $onset.dcaeTopic + "] -> PDP-D" );                   
		PolicyEngine.manager.deliver($onset.dcaeTopic, $onset.toDcaeOnset());
		insert(new AppcResponseEvent($onset.requestID.toString(), $onset.appcTopic, $onset.appcResponseCode));
	} catch (Exception e) {
		e.printStackTrace();
	} finally {
		retract($onset);
	}	
end

rule "vFWsim.APPC.RESPONSE"
when
	$appcResponse : AppcResponseEvent( code > 0 )
	$request : Request( getCommonHeader().RequestID.toString() == $appcResponse.requestID )
then
	String WHERE = drools.getRule().getPackage() + "." + drools.getRule().getName();
	
	try {
		System.out.println(WHERE + ": " + "APPC[" + $appcResponse.code + "|" + $appcResponse.requestID + "|" + 
		                   $appcResponse.appcTopic + "] -> PDP-D" );
		         
		PolicyEngine.manager.deliver($appcResponse.appcTopic, 
		                             AppcResponseEvent.toResponse($appcResponse.requestID, $appcResponse.code));
	} catch (Exception e) {
		e.printStackTrace();
	} finally {
		retract($appcResponse);
		retract($request);
	}
end
